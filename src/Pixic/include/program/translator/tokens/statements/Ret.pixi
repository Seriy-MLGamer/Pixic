/*
(C) 2022-2023 Серый MLGamer. All freedoms preserved.
Videos at Дзен: <https://dzen.ru/seriy_mlgamer>
Music at SoundCloud: <https://soundcloud.com/seriy_mlgamer>
Videos at YouTube: <https://www.youtube.com/@Seriy_MLGamer>
Code at GitHub: <https://github.com/Seriy-MLGamer>
Me at ВКонтакте: <https://vkontakte.ru/seriy_mlgamer>
Material support at DonationAlerts: <https://www.donationalerts.com/r/seriy_mlgamer>
E-mail: <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/translator/tokens/statements/Ret.pixi
 * class Ret extends Statement;
 *
 * Dependencies.
 * program/translator/tokens/statements/system/Statement.pixi
 * program/translator/Parser.pixi
 * program/translator/tokens/keywords.pixi
 * program/translator/character filters.pixi
 * program/translator/generator.pixi
 * program/info/developer errors.pixi
 * program/translator/tokens/show.pixi
 */

fn Ret_new($parser)
{
	$self=Statement_new($parser)
	$self.f=Ret

	Parser_promise_position($parser)
	$test=new(3, 1, INT8)
	clean($test)
	copy($test, $parser.text, 0, $parser[0, $parser.top])
	if strstr($test, KEYWORD_RET)
	{
		remove($test)
		Parser_reject_position($parser)

		remove($self)
		ret(-1)
	}
	remove($test)
	$parser[0, $parser.top]+3
	Parser_promise_position($parser)
	if is_symbolic(Parser_get($parser))
	{
		Parser_reject_position($parser)
		Parser_reject_position($parser)

		remove($self)
		ret(-2)
	}
	Parser_reject_position($parser)
	Parser_resolve_position($parser)
	$token=Parser_parse_parenthesis($parser)
	if $token<0
	{
		$self.value=-1

		ret($self)
	}
	$self.value=$token

	ret($self)
}
Ret=new(1, 1, INT8)
Ret.generate=Ret_generate
Ret.show=Ret_show
Ret.remove=Ret_remove
fn Ret_generate($self)
{
	generator_deinit()
	developer_error_not_implemented("generation of return from function")
}
fn Ret_show($self)
{
	begin_object()
		indent() logf("\"type\": \"Ret\"") next_property()
		indent() Statement_show($self) next_property()
		indent() logf("\"value\": ")
			$value=$self.value
			if $value<0
			{
				logf("null")
			}
			else
			{
				$value.f.show($value)
			}
	end_object()
}
fn Ret_remove($self)
{
	$value=$self.value
	if $value>=0
	{
		$value.f.remove($value)
	}

	remove($self)
}