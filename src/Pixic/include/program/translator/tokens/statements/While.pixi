/*
(C) 2022-2023 Серый MLGamer. All freedoms preserved.
Videos at Дзен: <https://dzen.ru/seriy_mlgamer>
Music at SoundCloud: <https://soundcloud.com/seriy_mlgamer>
Videos at YouTube: <https://www.youtube.com/@Seriy_MLGamer>
Code at GitHub: <https://github.com/Seriy-MLGamer>
Me at ВКонтакте: <https://vkontakte.ru/seriy_mlgamer>
Material support at DonationAlerts: <https://www.donationalerts.com/r/seriy_mlgamer>
E-mail: <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/translator/tokens/statements/While.pixi
 * class While extends Statement;
 *
 * Dependencies.
 * program/translator/tokens/statements/system/Statement.pixi
 * program/translator/Parser.pixi
 * program/translator/tokens/keywords.pixi
 * program/translator/character filters.pixi
 * program/info/syntax errors.pixi
 * program/translator/tokens/statements/system/Block.pixi
 * program/translator/generator.pixi
 * program/info/developer errors.pixi
 * program/translator/tokens/show.pixi
 */

fn While_new($parser)
{
	$self=Statement_new($parser)
	$self.f=While

	Parser_promise_position($parser)
	$test=new(5, 1, INT8)
	clean($test)
	copy($test, $parser.text, 0, $parser[0, $parser.top])
	if strstr($test, KEYWORD_WHILE)
	{
		remove($test)
		Parser_reject_position($parser)

		remove($self)
		ret(-1)
	}
	remove($test)
	$parser[0, $parser.top]+5
	Parser_promise_position($parser)
	if is_symbolic(Parser_get($parser))
	{
		Parser_reject_position($parser)
		Parser_reject_position($parser)

		remove($self)
		ret(-2)
	}
	Parser_reject_position($parser)
	Parser_resolve_position($parser)
	if Parser_skip_spaces($parser)
	{
		$parser[1, $parser.top]-1
		syntax_error_expected_expression($parser)
	}
	$token=Parser_parse_expression($parser)
	if $token<0
	{
		syntax_error_expected_expression($parser)
	}
	$self.condition=$token
	$self.last_line=$parser[1, $parser.top]+1
	if Parser_skip_spaces($parser)
	{
		$parser[1, $parser.top]-1
		syntax_error_expected($parser, '{')
	}
	$token=Block_new($parser)
	if $token<0
	{
		syntax_error_expected($parser, '{')
	}
	$self.body=$token

	ret($self)
}
While=new(1, 1, INT8)
While.generate=While_generate
While.show=While_show
While.remove=While_remove
fn While_generate($self)
{
	generator_deinit()
	developer_error_not_implemented("\"while\" loop generation")
}
fn While_show($self)
{
	begin_object()
		indent() logf("\"type\": \"While\"") next_property()
		indent() Statement_show($self) next_property()
		indent() logf("\"condition\": ") $self.condition.f.show($self.condition) next_property()
		indent() logf("\"body\": ") Block_show($self.body)
	end_object()
}
fn While_remove($self)
{
	$self.condition.f.remove($self.condition)
	Block_remove($self.body)

	remove($self)
}