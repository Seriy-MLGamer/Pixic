/*
(C) 2022-2023 Серый MLGamer. All freedoms preserved.
Videos at Дзен: <https://dzen.ru/seriy_mlgamer>
Music at SoundCloud: <https://soundcloud.com/seriy_mlgamer>
Videos at YouTube: <https://www.youtube.com/@Seriy_MLGamer>
Code at GitHub: <https://github.com/Seriy-MLGamer>
Me at ВКонтакте: <https://vkontakte.ru/seriy_mlgamer>
Material support at DonationAlerts: <https://www.donationalerts.com/r/seriy_mlgamer>
E-mail: <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/translator/tokens/operands/String.pixi
 * class String extends Operand;
 *
 * Include before.
 * program/translator/tokens/operands/system/Operand.pixi
 * Dependencies.
 * program/translator/Parser.pixi
 * program/info/syntax errors.pixi
 * program/translator/generator.pixi
 * program/info/developer errors.pixi
 * program/translator/tokens/show.pixi
 */

fn String_new($parser)
{
	$self=Operand_new(0, 0)
	$self.f=String

	Parser_promise_position($parser)
	$self.text=$parser.text
	if Parser_get($parser)!='"'
	{
		Parser_reject_position($parser)

		remove($self)
		ret(-1)
	}
	Parser_resolve_position($parser)
	$self.string_start=$parser[0, $parser.top]
	$backslash=0
	while 1
	{
		$character=Parser_get($parser)
		if $backslash
		{
			$backslash=0
		}
		else{if $character=='"'
		{
			$self.string_length=$parser[0, $parser.top]-1-$self.string_start

			ret($self)
		}
		else{if $character=='\\'
		{
			$backslash=1
		}
		else{if $character==EOF
		{
			if $parser[1, $parser.top]==get_ysize($parser.lines)
			{
				$parser[1, $parser.top]-1
				syntax_error_expected($parser, '"')
			}
		}}}}
	}
}
String=clone(Operand)
String.generate=String_generate
String.show=String_show
String.remove=String_remove
fn String_generate($self)
{
	generator_deinit()
	developer_error_not_implemented("string generation")
}
fn String_show($self)
{
	begin_object()
		indent() logf("\"type\": \"String\"") next_property()
		indent()
			$string=new($self.string_length, 1, INT8)
			copy($string, $self.text, 0, $self.string_start)
			logf("\"string\": \"%s\"", $string)
			remove($string)
	end_object()
}
fn String_remove($self)
{
	remove($self)
}