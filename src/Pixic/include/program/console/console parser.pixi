/*
(C) 2022-2023 Серый MLGamer. All freedoms preserved.
Videos at Дзен: <https://dzen.ru/seriy_mlgamer>
Music at SoundCloud: <https://soundcloud.com/seriy_mlgamer>
Videos at YouTube: <https://www.youtube.com/@Seriy_MLGamer>
Code at GitHub: <https://github.com/Seriy-MLGamer>
Me at ВКонтакте: <https://vkontakte.ru/seriy_mlgamer>
Material support at DonationAlerts: <https://www.donationalerts.com/r/seriy_mlgamer>
E-mail: <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/console/console parser.pixi
 * The console parser provides command line interface.
 *
 * Include before.
 * program/console/system/make arguments.pixi
 * configuration.pixi
 * Dependencies.
 * core/strings/file name.pixi
 * program/info/warnings.pixi
 * core/strings/find.pixi
 * program/info/errors.pixi
 */

STATE_NONE=  0
STATE_OUTPUT=1
STATE_FOLDER=2

input=-1
output=-1
folder=-1
translate_only=0
show_only=0

jump0=new(128, 1, INT)
clean(jump0)
jump0['-']=handle_extended_command
jump0['h']=help
jump0['l']=license
jump0['o']=set_output
jump0['f']=set_folder
jump0['t']=set_translate_only
jump0['s']=set_show_only
handle_extended_command_jump0=new(1, 1, INT8)
handle_extended_command_jump0.help=help
handle_extended_command_jump0.license=license
set_prop(handle_extended_command_jump0, "translate-only", set_translate_only)
set_prop(handle_extended_command_jump0, "show-only", set_show_only)

state=STATE_NONE
for (a=1; a!=arguments_count; a+1)
{
	argument=arguments[a]
	if state
	{
		if state==STATE_OUTPUT
		{
			if output<0
			{
				output=clone(argument)
				format_path(output)
			}
			else
			{
				warning_double_output_single(a)
			}
		}
		else{if state==STATE_FOLDER
		{
			if folder<0
			{
				folder=clone(argument)
				format_path(folder)
			}
			else
			{
				warning_double_folder_single(a)
			}
		}}
		state=STATE_NONE
	}
	else{if argument[0]=='-'
	{
		jump=jump0[argument[1]]
		if jump
		{
			state=jump(a)
		}
		else
		{
			warning_unknown_parameter(a)
		}
	}
	else{if input<0
	{
		input=clone(argument)
		format_path(input)
	}
	else
	{
		warning_double_input(a)
	}}}
}
remove(jump0)

if input<0
{
	if translate_only
	{
		if folder<0
		{
			if output<0
			{
				folder="boot"
			}
			else
			{
				folder=output
			}
		}
	}
	else
	{
		if output<0
		{
			if OS!=OS_WINDOWS
			{
				output="boot"
			}
			else
			{
				output="boot.exe"
			}
		}
		if folder<0
		{
			folder=clone(PIXIC_TEMP_PATH)
			strcat(folder, "boot")
		}
	}
}
else{if !translate_only&&output<0||folder<0
{
	extension=get_file_extension(input)
	if strcmp_no_case(extension, "pixi")&&strcmp_no_case(extension, "txt")||extension<0
	{
		name=get_full_file_name(input)
	}
	else
	{
		name=get_file_name(input)
	}
	if name<0
	{
		error_invalid_input()
	}
	remove(extension)
	if translate_only
	{
		if folder<0
		{
			if output<0
			{
				folder=clone(name)
			}
			else
			{
				folder=output
			}
		}
	}
	else
	{
		if output<0
		{
			output=clone(name)
			if OS==OS_WINDOWS
			{
				strcat(output, ".exe")
			}
		}
		if folder<0
		{
			folder=clone(PIXIC_TEMP_PATH)
			strcat(folder, name)
		}
	}
	remove(name)
}}

fn handle_extended_command($arg)
{
	$argument=arguments[$arg]
	$test=new(15, 1, INT8)
	clean($test)
	copy($test, $argument, 0, 2)
	$jump=get_prop(handle_extended_command_jump0, $test)
	if $jump
	{
		remove($test)
		ret($jump())
	}
	if !(strstr($test, "output")||($argument[8]&&$argument[8]!='='))
	{
		if $argument[8]!='='
		{
			warning_expected_assign($arg)
		}
		else{if $argument[9]
		{
			if output<0
			{
				output=new(get_size($argument)-9, 1, INT8)
				copy(output, $argument, 0, 9)
				format_path(output)
			}
			else
			{
				warning_double_output_extended($arg)
			}
		}
		else
		{
			warning_undefined_parameter($arg)
		}}
	}
	else{if !(strstr($test, "folder")||($argument[8]&&$argument[8]!='='))
	{
		if $argument[8]!='='
		{
			warning_expected_assign($arg)
		}
		else{if $argument[9]
		{
			if folder<0
			{
				folder=new(get_size($argument)-9, 1, INT8)
				copy(folder, $argument, 0, 9)
				format_path(folder)
			}
			else
			{
				warning_double_folder_extended($arg)
			}
		}
		else
		{
			warning_undefined_parameter($arg)
		}}
	}
	else
	{
		warning_unknown_parameter($arg)
	}}
	remove($test)
	ret(STATE_NONE)
}
fn help()
{
	if OS!=OS_WINDOWS
	{
		printf(text_help, "", PIXIC_TEMP_PATH)
	}
	else
	{
		printf(text_help, ".exe", PIXIC_TEMP_PATH)
	}
	exit(0)
}
fn license()
{
	printf(text_license)
	exit(0)
}
fn set_output($arg)
{
	$argument=arguments[$arg]
	if $argument[2]
	{
		if output<0
		{
			output=new(get_size($argument)-2, 1, INT8)
			copy(output, $argument, 0, 2)
			format_path(output)
		}
		else
		{
			warning_double_output_extended($arg)
		}
		ret(STATE_NONE)
	}
	ret(STATE_OUTPUT)
}
fn set_folder($arg)
{
	$argument=arguments[$arg]
	if $argument[2]
	{
		if folder<0
		{
			folder=new(get_size($argument)-2, 1, INT8)
			copy(folder, $argument, 0, 2)
			format_path(folder)
		}
		else
		{
			warning_double_folder_extended($arg)
		}
		ret(STATE_NONE)
	}
	ret(STATE_FOLDER)
}
fn set_translate_only()
{
	translate_only=1
	ret(STATE_NONE)
}
fn set_show_only()
{
	show_only=1
	ret(STATE_NONE)
}